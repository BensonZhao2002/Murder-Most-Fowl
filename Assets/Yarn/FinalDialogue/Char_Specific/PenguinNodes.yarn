title: PenguinIntroduction
tags: lc:Owl rc:
---
Athena:<i> This is that bellboy that stopped me earlier. He looks so nervous…</i>
Athena: <i>Makes sense. If I worked here, I would be nervous too.</i>
Athena: <i>Poor kid.</i>
<<set_character_idle_node Penguin PenguinProof>>
<<jump PenguinProof>>
===

title:PenguinConversation
tags: c:Owl:Penguin
---
<<set $ConvoChar = "Penguin">>
Skipper: Is there any way I can help?#e:Happy#s:Penguin
-> Where were you last night?
	Skipper: Hmm.. Well I spent a lot of time searching for my keys last night.#e:Neutral
	Athena: Your keys? What happened to them?
	Skipper: I believe I misplaced them. Though...
	Skipper: ...#e:Sad
	Skipper: Now that I think about it.. <color=\#f3db82>that eve Mallard offered to hang my coat up for me.</color>#e:Neutral
	Skipper: I didn't think much of it at the time, but I believe my keys went missing around that time.
	Athena: Did you ever find your keys?
	Skipper: I did not, so I asked Morgana to lock up the parlor.#e:Sad
	Athena: Did you notice anything else last night?
	Skipper: <color=\#f3db82>I heard a crash in the middle of the night</color>, but I did not have the energy to deal with it.#e:Neutral
	Skipper: I swear I tied all the luggage down properly.
	Skipper: Though... I could've missed something when <color=\#f3db82>Melissa came to get something from her luggage</color>.
	Athena: What about the morning? 
	Skipper: I woke to the sound of chatter outside.
	Skipper: I hadn't slept well so I was pretty groggy.
	Skipper: When Morgana unlocked the parlor door in the morning and the body was found, I took charge of the situation.
	Skipper: It's my job to ensure the safety of this train, which is why I wanted to handle it.
	<<jump PenguinConversation>>
-> What are your thoughts about Mallard?
	Skipper: ...#e:Neutral
	Skipper: Honestly I don't like him.#e:Sad
	Skipper: He's quite rude and snobbish.
	Skipper: He came from great wealth, so he probably grew up with everything going his way.#e:Neutral
	Skipper: However.. I feel like there was something off about him.
	Athena: Have you interacted with him much?
	Skipper: Thankfully I haven't had to deal with him much when I worked with Mr. Gander.
	Athena: What do you know about Mallard and Gander?
	Skipper: I know they started the company together, but if you want to know more, Mr. Gander would be the one to ask.
	: Got Evidence <color=\#94e9e4>Gander's Business Partner</color>
	<<CreateClue ClueBoardCanvas GooseBusinessPartner>>
	<<jump PenguinConversation>>
-> Do you know if anyone would want to kill him?#e:Neutral
	Skipper: Oh he knew how to make enemies.
	Skipper: In fact I believe he's involved with everyone in this car in some way.
	Skipper: I can tell you about whoever you wish.
	<<jump PenguinEnemies>>
-> <color=\#94e9e4>Present</color>
	<<PresentEvidence>>
-> Not now
	Athena: Thank you for your help. I'll be back later.
	Skipper: Glad to help!#e:Happy
	Skipper: If you need anything, I'll be in the luggage room.#e:Neutral
===

title: PenguinEnemies
tags: c:Owl:Penguin
---
Athena: Tell me about...
-> Chase Gander
	Skipper: Ah, Gander.#e:Neutral
	Skipper: Mallard definitely.. left on bad terms with him.#e:Sad
	Skipper: I'm not sure exactly why, but Mr. Gander had told that duck <color=\#f3db82>he needs to find a new job</color>.
	Skipper: Kicked the poor guy off to the streets.
	Skipper: Would you like to know about anyone else?#e:Neutral
	<<jump PenguinEnemies>>
-> Melissa Peacock
	Skipper: Melissa comes across as the most suspicious to me.#e:Neutral
	Athena: Oh?#el:Surprised
	Skipper: I overheard those two having a quite.. extreme argument the other day.#e:Sad
	Skipper: I couldn't make out a lot of what they were saying, but what I did overhear was concerning to say the least.
	Athena: What did you hear?#el:Neutral
	Skipper: I heard phrases such as <color=\#f3db82>"...RUINING PEOPLE'S LIVES"..</color>#e:Neutral
	Skipper: and <color=\#f3db82>"...MONEY OR YOU'RE CLUCKING DEAD"</color>.
	Athena: Coming from Melissa? I find that kind of hard to believe.#el:Intense
	Skipper: It's hard for me to believe too, but I remember what I heard.
	Athena: Perhaps I should <color=\#f3db82>ask Melissa</color> about this...
	<<set $PeacockArgument to true>>
	Skipper: Would you like to know about anyone else?#el:Neutral
	<<jump PenguinEnemies>>
-> Morgana Noir
	Skipper: Well Mallard would complain to me about her messing up his drinks constantly.#e:Neutral
	Skipper: Morgana is a good bartender. If she messed up his drink that many times it must've been out of spite.
	Skipper: Would you like to know about anyone else?
	<<jump PenguinEnemies>>
-> You.
	Skipper: Me?#e:Neutral
	Skipper: I mean I knew him. 
	Skipper: We were never really close.
	Skipper: ...#e:Sad
	Skipper: Would you like to know about anyone else?
	<<jump PenguinEnemies>>
-> That's all.
	Athena: That's all the questions about connections I have for now.
	Skipper: Alright!#e:Happy
	<<jump PenguinConversation>>
===

title:PenguinDismissal
---
Skipper: I don't know anything about this.
===

title:PenguinProof
tags: c:Owl:Penguin
---
<<set $ConvoChar = "Penguin">>
<<if $Tutorial is false>>
	<<jump PenguinConversation>>
<<endif>>
Skipper: Hello Detective. Do you have evidence for me?
-> I do
	<<jump ProofIdleNode1>>
-> Leave
===

title:ProofIdleNode1
---
Skipper: What makes you think it was <color=\#f3db82>impossible</color> for Mallard to have committed the act alone?
-> <color=\#94e9e4>Present</color>
    <<PresentEvidence Stool ProofWrong1>>
-> Leave
===

title:PenguinStool
---
<<if not $Tutorial>>
    <<jump PenguinDismissal>>
<<endif>>
Athena: This stool is way too short for the duck to have reached the chandelier himself.
Athena: He must've been hung up by someone else. 
Athena: This stool was placed by the murderer to make the scene look more convincing.
<<set_character_idle_node Penguin PenguinProof2>>
<<jump PenguinProof2>>
===

title:ProofWrong1
---
Skipper: I don't follow.#e:Sad
Athena: I'll see if I can find anything more relevant.
===

title:PenguinProof2
tags: c:Owl:Penguin
---
<<set $ConvoChar = "Penguin">>
Skipper: If that's the case, what was his <color=\#f3db82>cause of death</color>?
<<jump ProofIdleNode2>>
===

title:ProofIdleNode2
---
-> <color=\#94e9e4>Present</color>
    <<PresentEvidence Rope ProofWrong2>>
-> Leave
===

title:PenguinRope
---
<<if not $Tutorial>>
    <<jump PenguinDismissal>>
<<endif>>
Athena: He was certainly hung. The suffocation is clear.
Athena: However, there are no signs of struggle, even as he suffocated.
Athena: He must have been unconscious while he died, meaning he couldn't have hung himself.
<<set_character_idle_node Penguin PenguinProof3>>
<<jump PenguinProof3>>
===

title:ProofWrong2
---
Athena: Because he… uh…#el:Surprised
Skipper: I'll give you more time.#el:Neutral
===

title:PenguinProof3
tags: c:Owl:Penguin
position: 120,120
---
<<set $ConvoChar = "Penguin">>
Skipper: I see. What was he <color=\#f3db82>knocked out with</color>?
<<jump ProofIdleNode3>>
===

title:ProofIdleNode3
---
-> <color=\#94e9e4>Present</color>
    <<PresentEvidence Bottle ProofWrong3>>
-> Leave
===

title:PenguinBottle
tags: c:Owl:Penguin
---
<<if not $Tutorial>>
    <<jump PenguinDismissal>>
<<endif>>
Athena: There's a wine bottle missing from the bar cabinet.
Athena: Glass shards matching the glass were found next to the body.
Athena: I believe the duck was hit over the head with the bottle, then his unconscious body was hung and strangled to make it look like a suicide.
<<jump PenguinConclusion>>
===

title:ProofWrong3
---
Skipper: Hmm.. I'm not sure if that's a weapon...
Skipper: Perhaps you could <color=\#f3db82>ask some of the passengers about your evidence</color>.
Skipper: That could <color=\#f3db82>shed some light</color> on your evidence.
Athena: Thanks. I'll try that.
===

title:PenguinConclusion
---
Skipper: Interesting… 
Skipper: I think you have a good case, Ms..?
Athena: Athena. Athena Nox.
<<set $Tutorial to false>>
<<set_character_idle_node Penguin PenguinConversation>>
<<CreateClue ClueBoardCanvas AttorneyLetter>>
<<CreateClue ClueBoardCanvas SootyGloves>>
<<CreateClue ClueBoardCanvas PeacockReed>>
<<CreateClue ClueBoardCanvas DuckReed>>
<<CreateClue ClueBoardCanvas LoveLetter>>
<<CreateClue ClueBoardCanvas MaidenPhoto>>
<<CreateClue ClueBoardCanvas LoversPhoto>>
<<CreateClue ClueBoardCanvas FightersPhoto>>
<<CreateClue ClueBoardCanvas CrackCorn>>
<<CreateClue ClueBoardCanvas Newspaper>>
<<CreateClue ClueBoardCanvas Id>>
<<CreateClue ClueBoardCanvas Note>>
<<ChangeRightCharacter>>
<<ChangeLeftCharacter>>
:For the purposes of this demo, not all clues in the overworld are able to be obtained.
:In the final version these clues will be found in the bedrooms of the characters, which is currently inaccessibe.
:These unobtainable clues have been added to your inventory, so you can still enjoy the full story!
:Feel free to play around with them and test them all out!
Athena: Have fun!
Skipper: Thank you Detective Nox. I'll give you some time to investigate.#e:Happy
Skipper: Feel free to <color=\#f3db82>investigate the body</color> if you deem necessary.
Skipper: Be sure to <color=\#f3db82>question everyone thoroughly</color>.
Skipper: If you have any questions for me, I'm happy to answer.
Skipper: Let me know when you're ready to call the police, and you can turn in your suspect to them.#e:Neutral
===

title: PenguinKey
---
Skipper: Oh! That's my key! Where did you find it?#e:Angry
Athena: I found them in the victim's pocket...
<<CreateClue ClueBoardCanvas PenguinKey>>
: Got Evidence <color=\#94e9e4>Skipper's Key</color>
Skipper: Oh. I had a feeling he was the one who took them...#e:Sad
Skipper: ...
Skipper: Now that we have my keys, you can use these to search the rest of the car.#e:Neutral
Skipper: Oh also! Here take this cargo manifest.
Skipper: It may be useful in your investigation.
<<CreateClue  ClueBoardCanvas CargoManifest>>
: Got Evidence <color=\#94e9e4>Cargo Manifest</color>#el:Neutral
Athena: Thank you, Skipper.
Skipper: Likewise, detective.
===

title: PenguinBurntNote
---
Skipper: That and the noose made me think this was nothing but a suicide...#e:Neutral
Skipper: ...#e:Angry
Skipper: A part of me wishes that was never proven wrong.
Athena: You <color=\#f3db82>saw the note</color>?
Skipper: ...
Skipper: Yes I noticed it, but thought you would notice it as well, detective.#e:Neutral
===

title: PenguinManifest
---
Skipper: As I said, nothing in the cargo room is missing.#e:Neutral
===

title: PenguinMissingBriefCase
---
Athena: Are you sure nothing is missing?#el:Intense#e:Neutral
Skipper: I don't believe so.
Athena: Melissa remembers a quite heavy briefcase in the cargo hold...
Athena: However, his bag is absent in the manifest.
Skipper: ...#e:Angry
Skipper: Perhaps I missed something.#e:Happy
Skipper: People routinely store things in the back without telling me. #e:Neutral
Skipper: They try to get around the luggage prices...
===

title: PenguinCrackCorn
---
Skipper: When I was organizing the luggage, this <color=\#f3db82>fell out of Mallard's suitcase</color>.#el:Neutral#e:Neutral
Skipper: I wasn't trying to pry, I swear.
Skipper: However, I couldn't leave such an illegal drug laying out in the cargo bay.#e:Sad
Skipper: I had to remove it.#e:Neutral
===

title: PenguinCrowEx
---
Skipper: She and the victim used to date, yes.#el:Neutral#e:Neutral
Skipper: She talks about you way more though.
Athena: She does? #el:Surprised
Skipper: I think she wishes <color=\#f3db82>you'd apologize about that letter</color>.#e:Sad
Skipper: Quite a cruel way to cut someone off, isn't it?
Athena: ...#el:Sad
===

title: PenguinNewspaper
---
Skipper: They were inseperable <color=\#f3db82>partners in crime</color>- er I mean business.#el:Neutral
===

title: PenguinPenguinKey
---
Skipper: Those are my keys.#el:Neutral
Skipper: I lend them to you for your investigation.
Skipper: Do not abuse this power.
===

title: PenguinBriefcase
---
Athena: <i>I have a hunch it's his. I'm not 100% sure, but maybe if I pretend I know and press him enough, he'll crack.</i>#el:Intense
Athena: I'll ask one time. Is this yours.
Skipper: I-
Skipper: No.
Athena: Don't mess with me. I know it's yours.
Skipper: It's mine, ok. But the drugs aren't! I don't know where that came from!#e:Angry
Athena: I didn't <color=\#f3db82>tell you about the contents</color>.
Athena: <color=\#f3db82>How did you know there were drugs in the suitcase</color>?
Skipper: ... A wild guess! #e:Nervous
Skipper: I'm being interrogated about a suspicious briefcase! Of course there are drugs involved.
Athena: Why would Mallard have your briefcase?
Skipper: Huh? How did you kn-
Skipper: It's Mallard's?
Athena: No. It's yours. Why did Mallard have it?
Skipper: ...
Skipper: I didn't do it.#e:Angry
Athena: Answer my question.
Skipper: He must've... Must've taken my briefcase and filled it with drugs!
Athena: Cut the crap. I know the drugs are yours.
Athena: I'll ask one more time. What happened with the briefcase?
Skipper:...#e:Sad
Skipper: I was <color=\#f3db82>smuggling the drugs on the train</color>...
Skipper: I heard a crash in the middle of the night, and found Mallard with my briefcase.
Skipper: Mallard was still... bitter about his termination.
Skipper: He thought the briefcase was Gander's, and claimed he's just getting what he deserved.
Skipper: I didn't tell anyone, because I was afraid.
Skipper: Please don't turn me in.
Skipper: I didn't kill him.
Athena: There was white powder on the knife in his pocket.
Athena: He must have <color=\#f3db82>stolen the briefcase</color> before he was murdered.
Athena: And there was a stab mark on the table from the knife.
Athena: If someone caught him, it was not peaceful.
Skipper: It was an accide-#e:Nervous
Skipper: ...#e:Neutral
Skipper: I'm sure it's unrelated.#e:Neutral
Skipper: Do you have any more questions?
Athena: No. I think that's all I need.
===